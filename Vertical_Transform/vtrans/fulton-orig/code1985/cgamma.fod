      SUBROUTINE CGEVAL( RGAMMA, RTB, PB, PT, TOL, N, C, PSIHAT, IERR )
C
C       ****************** DOUBLE PRECISION VERSION ******************
C
C   PURPOSE
C
C       COMPUTES THE EIGENVALUES (PHASE SPEEDS) OF THE VERTICAL
C       STRUCTURE PROBLEM FOR CONSTANT  GAMMA = DT/DLOGP + KAPPA*T.
C
C   ARGUMENTS
C
C       DIMENSION   C(0:N), PSIHAT(0:N)
C
C       INPUT
C
C           RGAMMA  GAS CONSTANT  R  TIMES THE STATIC STABILITY  GAMMA
C
C           RTB     GAS CONSTANT  R  TIMES THE SURFACE TEMPERATURE  TB
C
C           PB, PT  BOTTOM AND TOP PRESSURES
C
C           TOL     TOLERANCE FOR COMPUTING THE PHASE SPEEDS  C(K):
C                       TOL.GT.0    USE ABSOLUTE TOLERANCE  TOL
C                       TOL.LT.0    USE RELATIVE TOLERANCE -TOL
C                       TOL.EQ.0    USE RELATIVE TOLERANCE  1.0E-22
C
C           N       NUMBER OF VERTICAL MODES DESIRED
C
C       OUTPUT
C
C           C       PHASE SPEEDS OF THE VERTICAL MODES
C
C           PSIHAT  NORMALIZATION CONSTANTS FOR THE VERTICAL MODES
C
C           IERR    ERROR FLAG (ZERO ON NORMAL RETURN)
C
C   METHOD
C
C       NEWTON'S METHOD IS USED TO SOLVE THE TRANCENDENTAL EQUATIONS
C       WHICH DETERMINE THE PHASE SPEEDS.  THE NORMALIZATION CONSTANTS
C       FOR THE VERTICAL STRUCTURE FUNCTIONS ARE COMPUTED BY A
C       CONSTRUCTION INVOLVING THE GREEN'S FUNCTION.
C
C   ERROR CONDITIONS
C
C       IERR = 1    RGAMMA.LE.0.0
C       IERR = 2       RTB.LE.0.0
C       IERR = 3        PT.LE.0.0
C       IERR = 4        PB.LE.PT
C       IERR = 5         N.LE.0
C       IERR.LT.0   NEWTON'S METHOD FAILED TO CONVERGE FOR VERTICAL
C                   MODE  K = -(IERR+1)  (THE RESULTS FOR MODES
C                   K = 0  TO  K = -(IERR+2)  SHOULD BE CORRECT)
C
C   LANGUAGE    FORTRAN 77
C
C   HISTORY     WRITTEN BY SCOTT R. FULTON IN MARCH 1984
C               (DOUBLE PRECISION VERSION IN APRIL 1984)
C
C
      INTEGER  N, IERR
      REAL  RGAMMA, RTB, PB, PT, TOL, C(0:N), PSIHAT(0:N)
      INTEGER  I, K, NITMAX, NSTART
      DOUBLE PRECISION  ALPHA, BETA, COLD, CTOL, CVAL
      DOUBLE PRECISION  EPS, MU, NU, PI, Z, ZC, ZT
      DOUBLE PRECISION  F0, FN, F0P, FNP, G0, GN
      PARAMETER  ( NITMAX = 10 )
C
C   EIGENVALUE RELATIONS FOR EXTERNAL AND INTERNAL MODES
C
      F0( MU ) = -MU*(1.0-ALPHA/(2*MU*MU))*SINH(MU*ZT)-BETA*COSH(MU*ZT)
      FN( NU ) =  NU*(1.0+ALPHA/(2*NU*NU))*SIN( NU*ZT)-BETA*COS( NU*ZT)
C
C   DERIVATIVES OF EIGENVALUE RELATIONS FOR EXTERNAL AND INTERNAL MODES
C
      F0P( MU ) = -(1.0 + ALPHA/(2*MU*MU) + BETA*ZT)*SINH( MU*ZT )
     2               - MU*ZT*(1.0 - ALPHA/(2*MU*MU))*COSH( MU*ZT )
      FNP( NU ) =  (1.0 - ALPHA/(2*NU*NU) + BETA*ZT)*SIN(  NU*ZT )
     2               + NU*ZT*(1.0 + ALPHA/(2*NU*NU))*COS(  NU*ZT )
C
C   GREEN'S FUNCTIONS FOR EXTERNAL AND INTERNAL MODES
C
      G0( Z, MU ) = COSH( MU*Z ) - (ALPHA/MU)*SINH( MU*Z )
      GN( Z, NU ) = COS(  NU*Z ) - (ALPHA/NU)*SIN(  NU*Z )
C
C   ARGUMENT CHECKS
C
      IERR = 0
      IF ( RGAMMA.LE.0.0 )  IERR = 1
      IF (    RTB.LE.0.0 )  IERR = 2
      IF (     PT.LE.0.0 )  IERR = 3
      IF (     PB.LE.PT  )  IERR = 4
      IF (      N.LE.0   )  IERR = 5
      IF ( IERR.NE.0 )  RETURN
C
C   SET UP THE NECESSARY CONSTANTS
C
      BETA  = DBLE(RGAMMA)/DBLE(RTB)
      ALPHA = 0.5 - BETA
      IF ( TOL.GT.0.0 )  CTOL = TOL
      IF ( TOL.EQ.0.0 )  EPS  = 1.0E-22
      IF ( TOL.LT.0.0 )  EPS  = ABS( TOL )
      ZT = LOG( DBLE(PB)/DBLE(PT) )
      ZC = 2*BETA/ALPHA
C
C   CALCULATE THE EIGENVALUE AND NORMALIZATION CONSTANT (EXTERNAL MODE)
C
      IF ( ZT.GT.ZC )  THEN
          CVAL = SQRT( RTB*(1.0 - EXP( -ZT )) )
          MU = SQRT( 0.25 - RGAMMA/CVAL**2 )
          DO 10 I=1,NITMAX
              COLD = CVAL
              MU = MU - F0( MU )/F0P( MU )
              CVAL = SQRT( RGAMMA/(0.25 - MU*MU) )
              IF ( TOL.LE.0.0 )  CTOL = EPS*CVAL
              IF ( ABS( CVAL - COLD ).LE.CTOL )  GO TO 20
   10     CONTINUE
          IERR = -1
          RETURN
   20     C(0) = CVAL
          PSIHAT(0) = SQRT( -2*MU*G0(0.0D0,MU)/(F0P(MU)*G0(ZT,MU)) )
          NSTART = 1
      ELSE IF ( ZT.EQ.ZC )  THEN
          NU = 0.0
          C(0) = SQRT( 4*DBLE(RGAMMA) )
          PSIHAT(0) = SQRT(3*ALPHA/((1-2*ALPHA)*(1+2*ALPHA+4*ALPHA**2)))
          NSTART = 1
      ELSE
          NSTART = 0
      END IF
C
C   CALCULATE THE EIGENVALUE AND NORMALIZATION CONSTANT (INTERNAL MODES)
C
      PI = ACOS( -1.0D0 )
      DO 50 K=NSTART,N
          NU = K*PI/ZT
          CVAL = SQRT( RGAMMA/(0.25 + NU*NU) )
          DO 30 I=1,NITMAX
              COLD = CVAL
              NU = NU - FN( NU )/FNP( NU )
              CVAL = SQRT( RGAMMA/(0.25 + NU*NU) )
              IF ( TOL.LE.0.0 )  CTOL = EPS*CVAL
              IF ( ABS( CVAL - COLD ).LE.CTOL )  GO TO 40
   30     CONTINUE
          IERR = -(K + 1)
          RETURN
   40     C(K) = CVAL
          PSIHAT(K) = SQRT( 2*NU*GN(0.0D0,NU)/(FNP(NU)*GN(ZT,NU)) )
   50 CONTINUE
      RETURN
      END
      SUBROUTINE CGEFUN( K, NP, P, RGAMMA, RTB, PB, PT, C, PSIHAT, PSI )
C
C       ****************** DOUBLE PRECISION VERSION ******************
C
C   PURPOSE
C
C       COMPUTES THE EIGENFUNCTIONS OF THE VERTICAL STRUCTURE
C       PROBLEM FOR CONSTANT  GAMMA = DT/DLOGP + KAPPA*T.
C
C   ARGUMENTS
C
C       DIMENSION   P(NP), C(0:N), PSIHAT(0:N), PSI(NP)
C
C       INPUT
C
C           K       INDEX OF THE VERTICAL STRUCTURE FUNCTION DESIRED
C
C           NP      NUMBER OF PRESSURE LEVELS
C
C           P       ARRAY OF PRESSURE LEVELS
C
C           RGAMMA  GAS CONSTANT  R  TIMES THE STATIC STABILITY  GAMMA
C
C           RTB     GAS CONSTANT  R  TIMES THE SURFACE TEMPERATURE  TB
C
C           PB, PT  BOTTOM AND TOP PRESSURES
C
C           C       PHASE SPEEDS OF THE VERTICAL MODES
C
C           PSIHAT  NORMALIZATION CONSTANTS FOR THE VERTICAL MODES
C
C       OUTPUT
C
C           PSI     PSI(I)  IS THE VALUE OF VERTICAL STRUCTURE
C                   FUNCTION  K  AT  P = P(I)
C
C   USE
C
C       FIRST CALL  CGEVAL  TO COMPUTE  C  AND  PSIHAT.
C       THEN  CALL  CGEFUN  TO COMPUTE  PSI  AS DESIRED.
C
C   LANGUAGE    FORTRAN 77
C
C   HISTORY     WRITTEN BY SCOTT R. FULTON IN MARCH 1984
C               (DOUBLE PRECISION VERSION IN APRIL 1984)
C
C
      INTEGER  K, NP
      REAL  P(NP), RGAMMA, RTB, PB, PT, C(0:0), PSIHAT(0:0), PSI(NP)
      INTEGER  I
      DOUBLE PRECISION  ALPHA, BETA, DP, MU, NU, Z, ZC, ZT
      DOUBLE PRECISION  DPB, DRGAM, G0, GN
C
C   GREEN'S FUNCTIONS FOR EXTERNAL AND INTERNAL MODES
C
      G0( Z, MU ) = COSH( MU*Z ) - (ALPHA/MU)*SINH( MU*Z )
      GN( Z, NU ) = COS(  NU*Z ) - (ALPHA/NU)*SIN(  NU*Z )
C
C   SET UP THE NECESSARY CONSTANTS
C
      IF ( K.LT.0 )  RETURN
      DRGAM = RGAMMA
      BETA  = DRGAM/RTB
      ALPHA = 0.5 - BETA
      DPB = PB
      ZT = LOG( DPB/PT )
      ZC = 2*BETA/ALPHA
      DP = DPB - PT
C
C   CALCULATE THE EIGENFUNCTION (VERTICAL STRUCTURE FUNCTION) VALUES
C
      IF ( K.EQ.0 .AND. ZT.GT.ZC )  THEN
          MU = SQRT( 0.25 - DRGAM/DBLE(C(0))**2 )
          DO 10 I=1,NP
   10     PSI(I) = SQRT( DP/P(I) )*PSIHAT(0)*G0( LOG(DPB/P(I)), MU )
      ELSE IF ( K.EQ.0 .AND. ZT.EQ.ZC )  THEN
          DO 20 I=1,NP
   20     PSI(I) = SQRT( DP/P(I) )*PSIHAT(0)*(1.0 - ALPHA*LOG(DPB/P(I)))
      ELSE
          NU = SQRT( DRGAM/DBLE(C(K))**2 - 0.25 )
          DO 30 I=1,NP
   30     PSI(I) = SQRT( DP/P(I) )*PSIHAT(K)*GN( LOG(DPB/P(I)), NU )
      END IF
      RETURN
      END
